/*
 * Copyright (c) 2017-2019 Altimit Community Contributors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or imp
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

// 统一管控app信息
def VOLUME_AS_ARROW     = false // 音量控制上下
def myAppName = "蒸汽之都的少女侦探"
def myApplicationId = "rpgmv.detective.girl.of.the.steam.city"
def myVersionCode = 1
def myVersionName = "1.0"
// 存档导出保留多少份
def saveFileExportNum = 32


android {
    /**
     * Advanced Configuration
     */
    def BACK_BUTTON_QUITS   = true // Use back button to quit the app?
    def SHOW_FPS            = false // Display the FPS monitor?
    def FORCE_CANVAS        = false // Disables fast WebGL rendering when available
    def FORCE_NO_AUDIO      = false // Disables WebAudio

    // Android 9+ "Q" API 29
    def ANDROID_SDK_TARGET  = 29

    defaultConfig {
        /**
         * Project Identification
         */
        applicationId myApplicationId // Change this to your applicationId.
        versionCode myVersionCode     // Set this to a higher number for game updates.
        versionName myVersionName     // This is the version that the player sees.

        compileSdkVersion ANDROID_SDK_TARGET
        targetSdkVersion ANDROID_SDK_TARGET
    }

    buildTypes {
        buildTypes.each { buildType ->
            buildType.crunchPngs false // Disables PNG crunching for the release build type.
            buildType.buildConfigField "boolean",  "BACK_BUTTON_QUITS", String.valueOf(BACK_BUTTON_QUITS)
            buildType.buildConfigField "boolean",  "SHOW_FPS",          String.valueOf(SHOW_FPS)
            buildType.buildConfigField "boolean",  "FORCE_CANVAS",      String.valueOf(FORCE_CANVAS)
            buildType.buildConfigField "boolean",  "FORCE_NO_AUDIO",    String.valueOf(FORCE_NO_AUDIO)
            buildType.buildConfigField "boolean",  "VOLUME_AS_ARROW",    String.valueOf(VOLUME_AS_ARROW)

            buildType.buildConfigField "Integer",  "SAVE_FILE_EXPORT_NUM",    String.valueOf(saveFileExportNum)

            buildType.buildConfigField "String", "APP_NAME", "\"${myAppName}\""
            buildType.manifestPlaceholders = [my_app_name: "${myAppName}"]
        }
    }

    flavorDimensions "mv_android_client"

    productFlavors {
        webview {
            dimension "mv_android_client"
            minSdkVersion 19 // 19 是安卓4.4 // 14
        }

        // WARNING: As of February 2017 Crosswalk is no longer maintained https://crosswalk-project.org/blog/crosswalk-final-release.html
        zz_crosswalk {
            dimension "mv_android_client"
            minSdkVersion 19  // 16
            targetSdkVersion 23
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    //noinspection GradleDependency
    implementation group: 'commons-io', name: 'commons-io', version: '2.4'

    zz_crosswalkImplementation (name:'xwalk_core_library-23.53.589.4',ext:'aar')
}